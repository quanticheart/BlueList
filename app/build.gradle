plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
}

androidGitVersion {
    abis = ['armeabi': 1, 'armeabi-v7a': 2, 'arm64-v8a': 3, 'mips': 5, 'mips64': 6, 'x86': 8, 'x86_64': 9]
    baseCode 200000
    codeFormat 'MNNPPP'
    format "%tag%%-count%%-commit%%-branch%%-dirty%"
    onlyIn ''
    prefix ''
    tagPattern(/^$prefix[0-9]+.*/)
    untrackedIsDirty = true
}

configurations {
    ktlint
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    classpath = configurations.ktlint
    main = "com.pinterest.ktlint.Main"
    args "src/**/*.kt"
    // to generate report in checkstyle format prepend following args:
    // "--reporter=plain", "--reporter=checkstyle,output=${buildDir}/ktlint.xml"
    // see https://github.com/pinterest/ktlint#usage for more
}

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    classpath = configurations.ktlint
    main = "com.pinterest.ktlint.Main"
    args "-F", "src/**/*.kt"
}

android {
    compileSdkVersion rootProject.compileSdkVersion

    defaultConfig {
        resConfigs "pt"

        applicationId "com.quanticheart.bluelist"
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion
        versionName androidGitVersion.name()
        versionCode androidGitVersion.code()

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        archivesBaseName = "${applicationId}-v${versionName}-(${versionCode})"
    }

    signingConfigs {
        debug {
            keyAlias 'debug'
            keyPassword '123456'
            storeFile file('key/debugkey')
            storePassword '123456'
        }
        release {
            keyAlias 'debug'
            keyPassword '123456'
            storeFile file('key/debugkey')
            storePassword '123456'
        }
    }

    buildTypes {
        debug {
            splits.abi.enable = false
            splits.density.enable = false
            aaptOptions.cruncherEnabled = false
            ext.alwaysUpdateBuildId = false
            applicationIdSuffix ".debug"

            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            manifestPlaceholders = [
                    labelName: "@string/app_name_debug",
                    appIcon  : "@mipmap/ic_launcher"
            ]

            signingConfig signingConfigs.debug
        }
        release {
            ndk {
                abiFilters 'x86', 'x86_64', 'arm64-v8a', 'armeabi-v7a'
            }

            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            manifestPlaceholders = [
                    labelName: "@string/app_name",
                    appIcon  : "@mipmap/ic_launcher"
            ]

            signingConfig signingConfigs.release
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        dataBinding = true
    }

    // Auto Start Ktlint in Build APP Module
    check.dependsOn ktlint
    android.applicationVariants.all { variant ->
        if (variant.buildType.name == "release") {
            variant.javaCompiler.dependsOn(ktlint)
        }
    }
}

dependencies {
    implementation appLibs

    //Kotlin verify code
    ktlint(ktlintLib.sdk)

    implementation project(":core")
    implementation project(":domain")
    implementation project(":data")
}